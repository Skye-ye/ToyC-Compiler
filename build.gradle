plugins {
    id 'java'
    id 'antlr'
    id 'application'
}

application {
    mainClass = 'toyc.Compiler'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.2'
    implementation("org.apache.logging.log4j:log4j-api:2.24.3")
    implementation("org.apache.logging.log4j:log4j-core:2.24.3")
    implementation 'org.antlr:antlr4-runtime:4.13.2'
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

generateGrammarSource {
    outputDirectory = layout.buildDirectory.dir('generated-src/antlr/main/toyc').get().asFile
    arguments = [
            '-visitor',
            '-no-listener',
            '-long-messages',
            '-package', 'toyc',
            '-Xexact-output-dir',
    ]
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task dotGen(type: JavaExec) {
    dependsOn classes
    group = 'application'
    description = 'Generate DOT files for visualization'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'toyc.Compiler'
    environment 'GENERATE_DOT', 'true'
}